/*
 * Copyright 2017 Intershop Communications AG.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    // project plugins
    id 'java-gradle-plugin'
    id 'groovy'
    // test coverage
    id 'jacoco'

    // ide plugin
    id 'idea'

    // plugin for documentation
    id 'org.asciidoctor.convert' version '1.5.3'

    // publish plugin
    id 'maven-publish'

    // intershop version plugin
    id 'com.intershop.gradle.scmversion' version '3.3.0'

    // plugin for publishing to Gradle Portal
    id 'com.gradle.plugin-publish' version '0.9.9'

    // plugin for publishing to jcenter
    id 'com.jfrog.bintray' version '1.8.0'
}

scm {
    version {
        initialVersion = '1.0.0'
    }
}

group 'com.intershop.gradle.databasedumper'
description 'Database content export and import plugin for Intershop'
version = scm.version.version

sourceCompatibility = 1.8
targetCompatibility = 1.8

// set correct project status
if (project.version.endsWith('-SNAPSHOT')) {
    status = 'snapshot'
}

test {
    if(System.getenv('ORACLEJDBCURL')) {
        systemProperty 'oracleJDBCURL', System.getenv('ORACLEJDBCURL')
    }
    if(System.getenv('MSSQLJDBCURL')) {
        systemProperty 'mssqlJDBCURL', System.getenv('MSSQLJDBCURL')
    }
    if(System.getenv('ORACLEJDBCUSER')) {
        systemProperty 'oracleJDBCUser', System.getenv('ORACLEJDBCUSER')
    }
    if(System.getenv('ORACLEJDBCPASSWORD')) {
        systemProperty 'oracleJDBCPassword', System.getenv('ORACLEJDBCPASSWORD')
    }
    if(System.getenv('MSSQLJDBCUSER')) {
        systemProperty 'mssqlJDBCUser', System.getenv('MSSQLJDBCUSER')
    }
    if(System.getenv('MSSQLJDBCPASSWORD')) {
        systemProperty 'mssqlJDBCPassword', System.getenv('MSSQLJDBCPASSWORD')
    }

    // Gradle versions for test
    systemProperty 'intershop.gradle.versions', '4.3.1'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Dumper, a tool to create and read database-backups.'
        attributes 'Main-Class': 'com.intershop.databasedumper.DatabaseDumper'
    }
}

task copyAsciiDoc {
    def outputDir = file("$buildDir/tmp/asciidoctorSrc")
    def inputFiles = fileTree(dir: rootDir, include: '**/*.asciidoc')

    inputs.files inputFiles
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()

        copy {
            from inputFiles
            into outputDir
        }
    }
}

asciidoctorj {
    noDefaultRepositories = true
}

asciidoctor {
    sourceDir "${buildDir}/tmp/asciidoctorSrc"
    sources {
        include 'README.asciidoc'
    }

    backends 'html5', 'docbook'
    options doctype : 'article',
            ruby    : 'erubis'
    attributes  'latestRevision'     : project.version,
            'toc':'left',
            'toclevels':'2',
            'source-highlighter' : 'coderay',
            'icons': 'font',
            'setanchors':'true',
            'idprefix':'asciidoc_',
            'idseparator':'-',
            'docinfo1':'true'
}

asciidoctor.dependsOn copyAsciiDoc

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task groovydocJar(type: Jar) {
    dependsOn groovydoc
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

// additional sources artifact
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        intershopMvn(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            artifact groovydocJar {
                classifier "javadoc"
            }

            artifact(new File(buildDir, 'asciidoc/html5/README.html')) {
                classifier 'reference'
            }

            artifact(new File(buildDir, 'asciidoc/docbook/README.xml')) {
                classifier = 'docbook'
            }

            pom.withXml {
                def Node root = asNode()
                root.appendNode('name', project.name)
                root.appendNode('description', project.description)
                root.appendNode('url', "https:/gitlab.intershop.de/IntershopGradlePlugins/${project.name}")

                def scm = root.appendNode( 'scm' )
                scm.appendNode( 'url', "https://gitlab.intershop.de/IntershopGradlePlugins/${project.name}")
                scm.appendNode( 'connection', "scm:git:https://gitlab.intershop.de/IntershopGradlePlugins/${project.name}.git")

                def org = root.appendNode( 'organization' )
                org.appendNode( 'name', 'Intershop Communications' )
                org.appendNode( 'url', 'http://intershop.com' )

                def license = root.appendNode( 'licenses' ).appendNode( 'license' )
                license.appendNode( 'name', 'Apache License, Version 2.0' )
                license.appendNode( 'url', 'http://www.apache.org/licenses/LICENSE-2.0' )
                license.appendNode( 'distribution', 'repo' )
            }
        }
    }
}

publish.dependsOn asciidoctor

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'intershopcommunicationsag'

        licenses = ['Apache-2.0']
        vcsUrl = "https://github.com/IntershopCommunicationsAG/${project.name}"

        desc = project.description
        websiteUrl = "https://github.com/IntershopCommunicationsAG/${project.name}"
        issueTrackerUrl = "https://github.com/IntershopCommunicationsAG/${project.name}/issues"

        labels = ['intershop', 'gradle', 'plugin', 'build', 'isml']
        publicDownloadNumbers = true

        version {
            name = project.version
            desc = "${project.description} ${project.version}"
            released  = new Date()
            vcsTag = project.version
        }

        publications = ['intershopMvn']
    }
}

bintrayUpload.dependsOn asciidoctor

pluginBundle {
    website = "https://github.com/IntershopCommunicationsAG/${project.name}"
    vcsUrl = "https://github.com/IntershopCommunicationsAG/${project.name}"
    description = project.description
    tags = ['intershop', 'gradle', 'plugin', 'build', 'databasedumper']

    plugins {
        gradleTestPlugin {
            id = 'com.intershop.gradle.databasepumper'
            displayName = project.description
        }
    }
}

// Write the plugin's classpath to a file to share with the tests
// this is necessary to get the jdbc jar to the plugin test classpath
// it can be changed with gradle 4
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath, sourceSets.main.compileClasspath
    outputs.dir outputDir


    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = (sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath.filter {
            it.name.contains('ojdbc6') || it.name.contains('mssql-jdbc')
        }).join("\n")
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}

dependencies {
    compile 'ch.qos.logback:logback-core:1.2.3'
    compile 'org.slf4j:slf4j-simple:1.7.25'
    compile 'org.apache.commons:commons-dbcp2:2.1.1'
    compile 'org.apache.commons:commons-pool2:2.4.2'
    compile 'com.google.guava:guava:22.0'
    compile 'commons-cli:commons-cli:1.4'

    testCompile 'com.intershop.gradle.test:test-gradle-plugin:3.0.0'
    testCompile gradleTestKit()

    testCompile 'junit:junit:4.12'
    testCompile 'org.zapodot:embedded-db-junit:1.0-RC1'

    testRuntime 'com.h2database:h2:1.4.182'
    if(System.getenv('MSSQLJDBCURL')) {
        testRuntime 'com.microsoft.sqlserver:mssql-jdbc:6.2.1.jre8'
        // this is necessary to get the jdbc jar to the plugin test classpath
        // it can be changed with gradle 4
        compileOnly 'com.microsoft.sqlserver:mssql-jdbc:6.2.1.jre8'
    }

    if(System.getenv('ORACLEJDBCURL')) {
        // found at JCenter
        testRuntime 'com.oracle:ojdbc6:12.1.0.1-atlassian-hosted'
        // this is necessary to get the jdbc jar to the plugin test classpath
        // it can be changed with gradle 4
        compileOnly 'com.oracle:ojdbc6:12.1.0.1-atlassian-hosted'
    }

    testRuntime files(createClasspathManifest)
}

repositories {
    jcenter()
}
